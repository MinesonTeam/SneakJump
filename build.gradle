plugins {
    id("java")
    id("io.github.goooler.shadow") version "8.1.8"
}

group = 'kz.hxncus.mc'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

def spigot = project.property("spigot")
def lombok = project.property("lombok")
def bstats = project.property("bstats")
def junit = project.property("junit")
def mockito = project.property("mockito")

dependencies {
    // Spigot
    compileOnly("org.spigotmc:spigot-api:${spigot}-R0.1-SNAPSHOT")

    // Lombok
    compileOnly("org.projectlombok:lombok:${lombok}")
    annotationProcessor("org.projectlombok:lombok:${lombok}")

    // BStats
    implementation("org.bstats:bstats-bukkit:${bstats}")

    // JUnit and Mockito
    testImplementation("org.junit.jupiter:junit-jupiter:${junit}")
    testImplementation("org.mockito:mockito-core:${mockito}")
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier.set("")
    archiveFileName.set("${rootProject.name}-${version}.jar")
    relocate("org.bstats", project.group.toString() + ".sneakjump.metrics")
    mustRunAfter(clean)
}

def targetJavaVersion = 8

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
